apply plugin: 'com.android.application'
apply from: "$rootDir/android_base.gradle"
apply plugin: 'com.google.dagger.hilt.android'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'com.jraska.module.graph.assertion'
apply plugin: 'kotlin-kapt'


android {
    namespace AndroidConfig.applicationId
    defaultConfig {
        applicationId AndroidConfig.applicationId
    }
}

dependencies {
    implementation projects.appRes
    implementation projects.core.fragmentUi

    implementation projects.features.featureDashboard.ui
    implementation projects.features.featureDashboard.data

    implementation projects.features.featureSearch.ui

    implementation projects.features.featureRepository.data

    implementation projects.features.featureRepositoryDetails.ui

    implementation Libraries.hiltAndroid
    kapt Libraries.hiltCompiler

    implementation Libraries.androidxNavigation
    implementation Libraries.androidxNavigationFragment

    implementation Libraries.retrofit
    implementation Libraries.retrofitMoshi
    implementation Libraries.retrofitAdapter

    implementation Libraries.androidxCore
    implementation Libraries.androidxAppCompat
    implementation Libraries.material
    implementation Libraries.androidxConstraintLayout
}

moduleGraphAssert {
    maxHeight = 5
    allowed = [
        ":app -> :.*",
        ":.* -> :app-res",
        ":features:.* -> :core:.*",
        ":features:feature-.*:data -> :features:feature-.*:domain",
        ":features:feature-.*:domain -> :features:feature-.*:domain",
        ":features:feature-.*:ui -> :features:feature-.*:domain",
        ":features:feature-.*:ui -> :features:feature-.*:ui",
        ":features:feature-.*:ui -> :core:(fragment-ui|view)",
    ]
    restricted = [
        ":.* -X> :(app|moduleGenerator)",
        ":core:.* -X> :features:.*",
        ":app-res -X> :.*",
        ":features:feature-.*:(data|domain) -X> :core:(fragment-ui|view)",
        ":features:feature-.*:data -X> :features:feature-.*:(data|ui)",
        ":features:feature-.*:domain -X> :features:feature-.*:(data|ui)",
    ]
    assertOnAnyBuild = true
}

tasks.register("generateAppGraph") {
    Process process = "./appGraph.sh".execute()
    process.waitForProcessOutput(System.out, System.err)
}
